// JOB DEMOECLP ECLIPTIC COORDINATE TABLE
// OPTION LINK,SYM,LISTX
// EXEC PL/I
* PROCESS STMT,48C  (PRINT STATEMENT NUMBER IF ABEND)
 /* SAMPLE SCIENTIFIC ORIENTED PL/I PROGRAM */
 /* USES THE 48 CHARACTER SET SYMBOLS */
 /* DEMONSTRATES STREAM ORIENTED I/O */
 /*  LIST MODE-- */
 /*   (FIELDS) */
 /*    FIELDS WILL BE POSITIONED AT PRE-DETERMINED TAB STOPS WITH */
 /*       BUILT IN FORMATTING DEPENDING ON FIELD TYPE */
 /*    TAB STOPS CAN BE CHANGED BY ASSEMBLING THE IJKZL MACRO */
 /*    TO MAKE PERMANENT CATALOG TO RELOCATABLE LIBRARY AS IJKTLTB */
 /*    DEFAULT IS IJKZL (1,25,49,73,97,121,FF) */
 /*    DEFAULT IJKTLTB TABS ARE PRINT POSITIONS 1,25,49,73,97,121 */
 /*  EDIT MODE-- */
 /*   (FIELDS) (FORTRAN LIKE FORMAT CODES) */
 ECLIPT.. PROCEDURE OPTIONS(MAIN),.
   DECLARE DATE BUILTIN,
           TODAY CHAR(8) STATIC INIT ('  /  /  '),
           PAGE FIXED DEC(3,0),
           DECL (12),.
   SUBSTR(TODAY,1,2) = SUBSTR(DATE,3,2),.
   SUBSTR(TODAY,4,2) = SUBSTR(DATE,5,2),.
   SUBSTR(TODAY,7,2) = SUBSTR(DATE,1,2),.
   PAGE = 1,.
   PUT LIST (TODAY,'ECLIPTIC COORDINATE TABLE','PAGE',PAGE),.
   PUT SKIP(3) EDIT ('0000     0005     0010     0015     0020     0025
      0030     0035     0040     0045     0050     0055')
       (COLUMN(16), A),.
   PUT SKIP(2),.
   DO I = 100 TO 2400 BY 100,.
     RAH = (I-100) / 100,.
     DO J = 1 TO 12,.
       DECL(J) = SIND((RAH+(J-1)/12)*15)*23.445,.
     END,.
     PUT EDIT (I-100,DECL) (COLUMN(7), F(4), 12 F(9,2)),.
   END,.
 END ECLIPT,.
/*
// EXEC ASSEMBLY
         IJKZL (1,40,100,105,FF)
         END
/*
// EXEC LNKEDT
// EXEC
/&
