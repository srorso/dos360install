* $$JOB DEMOSAMC,,,BG
// JOB DEMOSAMC DEMONSTRATE DISK SEQUENTIAL ACCESS METHOD-CREATE
// OPTION LINK
// EXEC ASSEMBLY
         TITLE 'DEMONSTRATE DISK SEQUENTIAL ACCESS METHOD-CREATE'
* THIS PROGRAM GIVES AN EXAMPLE OF USAGE OF THE DISK SEQUENTIAL
* ACCESS METHOD.  IT BUILDS A FILE FROM A SEQUENTIAL FILE.
* IT ALSO SHOWS AN EXAMPLE OF A SELF-RELOCATING PROGRAM USING
* REGISTER NOTATION IN THE MACROS.
*        PRINT NOGEN
DEMOSAMC CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R12      EQU   12     BASE
R14      EQU   14
         BALR  R12,0
         USING *,R12
         OPENR READER,SDFILE,PRINTER   OPEN FILES
         COMRG                     GET TODAY'S DATE
         MVC   TODAY,0(R1)
         BAL   R3,HEADING          PRINT FIRST PAGE HEADING
*
MAINLOOP LA    R1,READER           READ A CARD
         GET   (1)
         CLI   EOPSW,C'1'          IF BOTTOM OF PAGE,
         BNE   *+8
         BAL   R3,HEADING             GO TO NEXT PAGE
         MVC   PRTLINE+11(L'WKREC),CARDIN
         BAL   R2,PRINT
         MVC   WKREC,CARDIN
         LA    R0,WKREC
         LA    R1,SDFILE           WRITE RECORD
         PUT   (1),(0)
         B     MAINLOOP
*
ENDOFJOB CLOSER READER,SDFILE,PRINTER  CLOSE FILES
         EOJ
*
HEADING  MVC   PRTLINE+1(L'TODAY),TODAY
         MVC   PRTLINE+30(L'MHEAD1),MHEAD1
         MVI   PRTLINE,C'1'         SKIP TO NEW PAGE
         BAL   R2,PRINT
         MVI   PRTLINE,C'0'         DOUBLE SPACE NEXT TIME
         MVI   EOPSW,C'0'           RESET END-OF-PAGE SWITCH
         BR    R3
PRINT    LA    R0,PRTLINE
         LA    R1,PRINTER
         PUT   (1),(0)
         LA    R0,EOP               
         PRTOV (1),12,(0)           CHECK FOR BOTTOM OF PAGE
         MVI   PRTLINE,C' '         CLEAR PRINT LINE
         MVC   PRTLINE+1(132),PRTLINE
         BR    R2
EOP      MVI   EOPSW,C'1'           INDICATE BOTTOM OF PAGE
         BR    R14
MHEAD1   DC    C'DISK SAM DEMONSTRATION'
TODAY    DS    CL8
EOPSW    DS    C
CARDIN   DS    CL80
WKREC    DS    CL75
PRTLINE  DC    CL133' '
         LTORG
READER   DTFDI DEVADDR=SYSIPT,EOFADDR=ENDOFJOB,                        X
               IOAREA1=CARDIN,RECSIZE=80
SDFILE   DTFSD DEVICE=2314,TYPEFLE=OUTPUT,VERIFY=YES,                  X
               RECFORM=FIXBLK,BLKSIZE=308,RECSIZE=75,                  X
               IOAREA1=SDIO1,IOAREA2=SDIO2,WORKA=YES
* NOTE FOR OUTPUT FILES THE BLKSIZE= IS RECSIZE= * BLK.FACTOR +8
*  TO ALLOW FOR COUNT FIELD.  THE OUTPUT AREAS NEED TO BE THIS LENGTH.
PRINTER  DTFPR DEVADDR=SYSLST,BLKSIZE=133,IOAREA1=PRNTOUT1,            X
               WORKA=YES,PRINTOV=YES,CTLCHR=ASA
SDIO1    DS    CL308
SDIO2    DS    CL308
PRNTOUT1 DS    CL133
         PRMOD WORKA=YES,CTLCHR=ASA,PRINTOV=YES
         END
/*
// EXEC LNKEDT
// DLBL SDFILE,'DEMO.SAM.FILE',0
// EXTENT SYS004,WRK14A,1,0,2600,20
// EXEC
10004ACHER, WILLIAM C.          00675241004            3679874321120000034
10185DONNEMAN, THOMAS M.        00900191003            1043781234130000040
10300FELDMAN, MIKE R.           00300000004            2156278643115000026
10325HATFIELD, MARK I.          00205390002            2225723628090000030
10730REEDE, OWEN W.             01051440001            2115897234105000021
10960WINGLAND, KEITH E.         00350000003            4215679672085000026
20111CARTOLER, VIOLET B.        00750060004            3455667381140000032
20304FROMM, STEVE V.            01200005002            2300267832122500037
20590NEIL, CLARENCE N.          00950230001            4016787112135000040
20801SCHEIBER, HARRY T.         00325080002            6198842236112500046
20956WANGLEY, THEO. A.          00150000003            1723456124120000050
30030ALLOREN, RUTH W.           00000000002            7647982367130000055
30181DELBERT, EDWARD D.         01305541015            6419182773110000051
30318HANEY, CAROL S.            01450005008            5533266791100000058
30487KING, MILDRED J.           01804290010            8711322487090500033
30834TRAWLEY, HARRIS T.         00550000009            7623561471100250032
40171COSTA, NAN S.              00560000005            1241963182122000046
40027ALHOUER, ELAINE E.         00220660006            6381469783079500022
40317HANBEE, ALETTA O.          00395000008            1136719674139000050
40721RASSMUSEN, JOHN J.         01000000004            2064397865129600040
50040ATKINSON, CHARLES          00675241004            3679874321120000034
50060BASEL, DEBORAH L           00900191003            1043781234130000040
50105BETTINARDI, RONALD J       01500500003            1125666601110000022
/*
/&
// JOB DEMOSAMR DEMONSTRATE DISK SEQUENTIAL ACCESS METHOD-RETRIEVE
// OPTION LINK
// EXEC ASSEMBLY
         TITLE 'DEMONSTRATE DISK SEQUENTIAL ACCESS METHOD-RETRIEVE'
*        PRINT NOGEN
DEMOSAMR CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R12      EQU   12    BASE
R14      EQU   14
         BALR  R12,0
         USING *,R12
         OPENR SDFILE,PRINTER      OPEN FILES
         COMRG                     GET TODAY'S DATE
         MVC   TODAY,0(R1)
         BAL   R3,HEADING          PRINT FIRST PAGE HEADING
*
MAINLOOP LA    R1,SDFILE
         LA    R0,WKREC
         GET   (1),(0)
         CLI   EOPSW,C'1'          IF BOTTOM OF PAGE,
         BNE   *+8
         BAL   R3,HEADING             GO TO NEXT PAGE
         MVC   PRTLINE+11(L'WKREC),WKREC
         BAL   R2,PRINT
         B     MAINLOOP
*
ENDOFJOB CLOSER SDFILE,PRINTER     CLOSE FILES
         EOJ
*
HEADING  MVC   PRTLINE+1(L'TODAY),TODAY
         MVC   PRTLINE+30(L'MHEAD1),MHEAD1
         MVI   PRTLINE,C'1'        SKIP TO NEW PAGE
         BAL   R2,PRINT
         MVI   PRTLINE,C'0'        DOUBLE SPACE NEXT TIME
         MVI   EOPSW,C'0'          RESET BOTTOM-OF-PAGE SWITCH
         BR    R3
PRINT    LA    R0,PRTLINE
         LA    R1,PRINTER
         PUT   (1),(0)
         LA    R0,EOP
         PRTOV (1),12,(0)          CHECK FOR BOTTOM OF PAGE
         MVI   PRTLINE,C' '
         MVC   PRTLINE+1(132),PRTLINE
         BR    R2
EOP      MVI   EOPSW,C'1'          INDICATE BOTTOM OF PAGE
         BR    R14
MHEAD1   DC    C'DISK SAM DEMONSTRATION'
TODAY    DS    CL8
EOPSW    DS    C
WKREC    DS    CL75
PRTLINE  DC    CL133' '
         LTORG
SDFILE   DTFSD DEVICE=2314,EOFADDR=ENDOFJOB,                           X
               RECFORM=FIXBLK,BLKSIZE=300,RECSIZE=75,                  X
               IOAREA1=SDIO1,IOAREA2=SDIO2,WORKA=YES
PRINTER  DTFPR DEVADDR=SYSLST,BLKSIZE=133,IOAREA1=PRNTOUT1,            X
               WORKA=YES,PRINTOV=YES,CTLCHR=ASA
SDIO1    DS    CL300
SDIO2    DS    CL300
PRNTOUT1 DS    CL133
         PRMOD WORKA=YES,CTLCHR=ASA,PRINTOV=YES
         END
/*
// EXEC LNKEDT
// DLBL SDFILE,'DEMO.SAM.FILE'
// EXTENT SYS004,WRK14A
// EXEC
/&
* $$EOJ