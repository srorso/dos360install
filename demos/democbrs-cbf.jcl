* $$JOB DEMOCBRS,,,BG
// JOB DEMOCBRS SAMPLE ANS COBOL PROGRAM - REPORT WRITER AND SORT
// OPTION LINK,SYM,LISTX
// EXEC FCOBOL
 CBL BUF=1024,SUPMAP,NOTRUNC
       IDENTIFICATION DIVISION.
       PROGRAM-ID. DMCBRS.
       AUTHOR. A PROGRAMMER.
      * THIS IS A SAMPLE ANS COBOL PROGRAM USING SORT, REPORT WRITER,
      *    SEGMENTATION, AND SORT CHECKPOINTING.
           SKIP1
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-360.
       OBJECT-COMPUTER. IBM-360.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT READER  ASSIGN TO SYS007-UR-2501-S.
           SELECT PRINTER ASSIGN TO SYS009-UR-1403-S.
           SELECT SORTWRK ASSIGN TO SYS001-UT-2314-S-SORTWK1.
       I-O-CONTROL.
           RERUN ON SYS000-UT-2314-S-SORTCKP.
           SKIP1
       DATA DIVISION.
       FILE SECTION.
       FD  READER
           LABEL RECORDS ARE OMITTED
           RECORD CONTAINS 80 CHARACTERS
           DATA RECORD IS SALES-RECORD.
       01  SALES-RECORD.
           05  IDEPTNO                 PIC XX.
           05  FILLER                  PIC X(78).
       FD  PRINTER
           LABEL RECORDS ARE OMITTED
           REPORT IS SALES-REPORT.
       SD  SORTWRK
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 80 CHARACTERS.
       01  SORT-RECORD.
           05  SDEPTNO                 PIC XX.
           05  SSALESNO                PIC XXX.
           05  SNAME                   PIC X(20).
           05  FILLER                  PIC X(7).
           05  SAMOUNT                 PIC S9(5)V99.
           05  FILLER                  PIC X(41).
       WORKING-STORAGE SECTION.
       01  FILLER.
           05  BELOW-FIELD             PIC X(11).
       REPORT SECTION.
       RD  SALES-REPORT  CONTROLS ARE FINAL SDEPTNO
           PAGE LIMIT 59 LINES
           HEADING 1
           FIRST DETAIL 5
           LAST DETAIL 48
           FOOTING 52.
       01  TYPE IS PAGE HEADING.
           05  LINE 1.
               10      COLUMN  1   PIC X(8)   SOURCE CURRENT-DATE.
               10      COLUMN 19   PIC X(12)  VALUE 'SALES REPORT'.
               10      COLUMN 43   PIC X(4)   VALUE 'PAGE'.
               10      COLUMN 47   PIC ZZZZ9  SOURCE PAGE-COUNTER.
           05  LINE 3.
               10      COLUMN  1   PIC X(51)  VALUE
                 'DEPT.NO.  SALES.NO.       NAME              AMOUNT'.
       01  DETAIL-LINE  TYPE IS DETAIL  LINE PLUS 1.
           05    GROUP COLUMN  4   PIC XX     SOURCE SDEPTNO.
           05          COLUMN 14   PIC XXX    SOURCE SSALESNO.
           05          COLUMN 22   PIC X(20)  SOURCE SNAME.
           05          COLUMN 44   PIC ZZ,ZZ9.99-  SOURCE SAMOUNT.
           05          COLUMN 56   PIC X(11)  SOURCE BELOW-FIELD.
       01  TYPE IS CONTROL FOOTING SDEPTNO  LINE PLUS 1
               NEXT GROUP PLUS 1.
           05          COLUMN 25   PIC X(16)  VALUE 'DEPARTMENT TOTAL'.
           05  DEPTOT  COLUMN 42   PIC ZZZZ,ZZ9.99-  SUM SAMOUNT.
       01  TYPE IS CONTROL FOOTING FINAL  LINE PLUS 2.
           05          COLUMN 25   PIC X(11)  VALUE 'FINAL TOTAL'.
           05          COLUMN 41   PIC $ZZZZ,ZZ9.99-  SUM DEPTOT.
           SKIP1
       PROCEDURE DIVISION.
           OPEN INPUT READER
                OUTPUT PRINTER.
           SORT SORTWRK
               ASCENDING KEY SDEPTNO, SSALESNO
      *        USING READER
               INPUT PROCEDURE IS SORT-IN
               OUTPUT PROCEDURE IS SORT-OUT.
           IF SORT-RETURN NOT = ZERO
              THEN DISPLAY 'BAD SORT' UPON CONSOLE.
           CLOSE READER, PRINTER.
           STOP RUN.
           SKIP1
       SORT-IN SECTION 70.
       READ-LOOP.
           READ READER  AT END GO TO EOFRDR.
           IF IDEPTNO = '20' OR '50'
              THEN MOVE SALES-RECORD TO SORT-RECORD
                   RELEASE SORT-RECORD.
           GO TO READ-LOOP.
       EOFRDR.
           EXIT.

       SORT-OUT SECTION 80.
           INITIATE SALES-REPORT.
       PRINT-LOOP.
           RETURN SORTWRK  AT END GO TO EOFSORT.
           IF SAMOUNT LESS THAN 1000.00
               THEN MOVE 'BELOW QUOTA' TO BELOW-FIELD
               ELSE MOVE SPACES TO BELOW-FIELD.
           GENERATE DETAIL-LINE.
           GO TO PRINT-LOOP.
       EOFSORT.
           TERMINATE SALES-REPORT.
/*
// EXEC LNKEDT
// ASSGN SYS007,X'00C'
// ASSGN SYS009,X'00E'
// ASSGN SYS000,X'192'
// DLBL SORTCKP,'SORT.CKPT',0
// EXTENT SYS000,WRK14A,1,1,2600,300
// ASSGN SYS001,X'192'
// DLBL SORTWK1,'DOS.BG.WORKFILE.001',0,SD
// EXTENT SYS001,WRK14A,1,1,1700,300
// EXEC
10004ACHER, WILLIAM C.          00675241004            3679874321120000034
10185DONNEMAN, THOMAS M.        00900191003            1043781234130000040
10300FELDMAN, MIKE R.           00300000004            2156278643115000026
10325HATFIELD, MARK I.          00205390002            2225723628090000030
10730REEDE, OWEN W.             01051440001            2115897234105000021
10960WINGLAND, KEITH E.         00350000003            4215679672085000026
20111CARTOLER, VIOLET B.        00750060004            3455667381140000032
20304FROMM, STEVE V.            01200005002            2300267832122500037
20590NEIL, CLARENCE N.          00950230001            4016787112135000040
20801SCHEIBER, HARRY T.         00325080002            6198842236112500046
20956WANGLEY, THEO. A.          00150000003            1723456124120000050
30030ALLOREN, RUTH W.           00000000002            7647982367130000055
30181DELBERT, EDWARD D.         01305541015            6419182773110000051
30318HANEY, CAROL S.            01450005008            5533266791100000058
30487KING, MILDRED J.           01804290010            8711322487090500033
30834TRAWLEY, HARRIS T.         00550000009            7623561471100250032
40171COSTA, NAN S.              00560000005            1241963182122000046
40027ALHOUER, ELAINE E.         00220660006            6381469783079500022
40317HANBEE, ALETTA O.          00395000008            1136719674139000050
40721RASSMUSEN, JOHN J.         01000000004            2064397865129600040
50040ATKINSON, CHARLES          00675241004            3679874321120000034
50060BASEL, DEBORAH L           00900191003            1043781234130000040
50105BETTINARDI, RONALD J       01500500003            1125666601110000022
/*
/&
// JOB DEMOCBRS RESTART FROM CHECKPOINT 2
// ASSGN SYS007,X'00C'
// ASSGN SYS009,X'00E'
// ASSGN SYS000,X'192'
// DLBL SORTCKP,'SORT.CKPT'
// EXTENT SYS000,WRK14A
// RSTRT SYS000,2,SORTCKP
/&
* $$EOJ